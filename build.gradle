/**************************************************
* Buildscript
**************************************************/
buildscript {
  ext {
    springBootVersion = "1.3.0.RELEASE"
  }
  
  repositories {
    mavenCentral()
  }
  
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

/**************************************************
* Plugins 
**************************************************/
plugins {
  id 'org.unbroken-dome.test-sets' version '1.1.0'
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "spring-boot"

/**************************************************
* Reports 
**************************************************/
apply from: "gradle/reports.gradle"

/**************************************************
* Repositories 
**************************************************/
repositories {
  mavenCentral()
  maven { url "https://repo.spring.io/snapshot" }
  maven { url "https://repo.spring.io/milestone" }
}

/**************************************************
* JAR 
**************************************************/
jar {
  baseName = "spring-boot-starter-app"
  version = "0.0.1-SNAPSHOT"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

/**************************************************
* Integration Tests
**************************************************/
testSets {
   integrationTest { dirName = 'integration-test' }
}

project.integrationTest {
  outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

/**************************************************
* Eclipse 
**************************************************/
eclipse {    
  classpath {
    containers.remove "org.eclipse.jdt.launching.JRE_CONTAINER"
    containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8"
  }
}

/**************************************************
* Heroku Deployment 
**************************************************/
task stage (dependsOn: [clean, build]) {}

/**************************************************
* Dependencies 
**************************************************/
dependencies {
  compile "org.apache.commons:commons-lang3:3.0"
  compile "org.projectlombok:lombok"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-data-mongodb"
  compile "org.springframework.boot:spring-boot-starter-data-rest"
  compile "org.springframework.boot:spring-boot-starter-web"
  
  integrationTestCompile "com.jayway.restassured:rest-assured:2.7.0"
  
  testCompile "com.google.guava:guava:19.0"
  testCompile "nl.jqno.equalsverifier:equalsverifier:1.7.5"
  testCompile "org.javassist:javassist:3.20.0-GA"
  testCompile "org.meanbean:meanbean:2.0.3"
  testCompile "org.springframework.boot:spring-boot-starter-test"
}

dependencyManagement {
  imports { 
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.M3" 
  }
}

/**************************************************
* Build Order 
**************************************************/
// Run clean before build (necessary for stage task)
build.mustRunAfter clean

// Run integration tests after unit tests
integrationTest.mustRunAfter test

// Run checks after tests
check.mustRunAfter test
check.mustRunAfter integrationTest

// Generate the code coverage report after unit tests are run, but before the integration tests are run
test.finalizedBy jacocoTestReport
integrationTest.mustRunAfter jacocoTestReport

// Generate javadoc after compiling the main source
compileJava.finalizedBy javadoc

/**************************************************
* Wrapper 
**************************************************/
task wrapper(type: Wrapper) {
  gradleVersion = "2.7"
}
